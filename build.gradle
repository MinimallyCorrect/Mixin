buildscript {
	repositories {
		jcenter()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies { classpath 'org.minimallycorrect.gradle:DefaultsPlugin:0.0.32' }
}
plugins {
	id 'java'
	id 'maven'
}
apply plugin: 'org.minimallycorrect.gradle.DefaultsPlugin'

version = '1.0.1-SNAPSHOT'

minimallyCorrectDefaults {
	description = "Applies Mixin-style monkey patches to .java source or compiled .class files"
	labels = ["mixin", "java", "aspect-oriented-programming", "bytecode-manipulation"]
}()

// Source compiler configuration
tasks.withType(JavaCompile) {
	sourceCompatibility = 8
	targetCompatibility = 8
	options.with {
		deprecation = true
		encoding = 'UTF-8'
		compilerArgs << "-Xlint:all" << "-Xlint:-path" << "-Xlint:-processing"
	}
}

repositories {
	jcenter()
	maven { url = 'https://repo.nallar.me/' }
}

configurations {
	hidden
	compile.extendsFrom hidden
	all {
		resolutionStrategy {
			failOnVersionConflict()

			force('com.google.code.findbugs:jsr305:3.0.2')

			cacheChangingModulesFor 10, 'seconds'
		}
	}
}

dependencies {
	testCompile "junit:junit:4.12"
	compile 'me.nallar.whocalled:WhoCalled:1.1'
	compile 'org.minimallycorrect.javatransformer:JavaTransformer:1.8.7'
}

// TODO: move into defaults plugin?
// don't like the javadoc rules, not having an @return when it's obvious is valid
if (JavaVersion.current().isJava8Compatible()) {
	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}
