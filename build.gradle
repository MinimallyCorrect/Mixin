plugins {
	id 'java'
	id 'maven'
}
project.version = '1.0-SNAPSHOT'
project.group = 'org.minimallycorrect.mixin'

// Extended project information
ext.projectName = 'Mixin'
ext.inceptionYear = '2016'
ext.packaging = 'jar'
ext.url = 'https://github.com/MinimallyCorrect/Mixin'
ext.description = 'Lightweight Mixins'
ext.organization = 'nallar.me'

// Define variables
ext.buildNumber = System.getenv("BUILD_NUMBER") ?: 0
ext.ciSystem = System.getenv("JENKINS_URL") ? 'Jenkins' : 'unknown'
ext.commit = System.getenv("GIT_COMMIT") ?: 'unknown'

// Source compiler configuration
tasks.withType(JavaCompile) {
	sourceCompatibility = 8
	targetCompatibility = 8
	options.with {
		deprecation = true
		encoding = 'UTF-8'
		compilerArgs << "-Xlint:all" << "-Xlint:-path" << "-Xlint:-processing"
	}
}

repositories {
	jcenter()
	maven { url='https://repo.nallar.me/' }
}

configurations {
	hidden
	compile.extendsFrom hidden
	all {
		resolutionStrategy {
			failOnVersionConflict()

			cacheChangingModulesFor 10, 'seconds'
		}
	}
}

dependencies {
	testCompile "junit:junit:4.12"
	compile 'me.nallar.whocalled:WhoCalled:1.1'
	compile 'org.minimallycorrect.javatransformer:JavaTransformer:1.8-SNAPSHOT'
	compileOnly 'org.projectlombok:lombok:1.16.18'
	testCompileOnly 'org.projectlombok:lombok:1.16.18'
}

jar {
	baseName = project.name
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar.manifest.mainAttributes(
	"Built-By": System.properties['user.name'],
	"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
	"Implementation-Title": name,
	"Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
	"Implementation-Vendor": url)

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}
